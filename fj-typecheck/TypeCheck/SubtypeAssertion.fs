module TypeCheck.SubtypeAssertion

open AST
open ClassTable
open Utils
open TypeCheck.SRefl
open TypeCheck.WFObject
open TypeCheck.Undecidable

let rec subtypeAssertion // ğš« âŠ¢ T <: U
    (subtype: Type) // T
    (supertype: Type) // U
    (visited: Visited)
    (state: State)
    =
    printfn $"{subtype |> debugType} <: {supertype |> debugType}"

    if visited |> List.contains (subtype, supertype) then
        Error "Infinite cycle"
    else
        if visited |> List.isEmpty |> not then
            undecidable (subtype, supertype) visited.Head state
        else
            Ok false
        |> Result.bind (fun undecidableResult ->
            if undecidableResult then
                Error "Undecidable"
            else
                let newVisited = (subtype, supertype) :: visited

                match supertype with
                | TypeVariable _ -> Error "Bounds cannot be type variables"
                | NonvariableType superNvType -> // U = D<UÌ„>
                    match subtype with
                    | TypeVariable subtypeVar -> bound subtypeVar superNvType newVisited state

                    // If T = C<TÌ„> and C = D, check if Var rule holds
                    | NonvariableType subNvType when subNvType.ClassName = superNvType.ClassName ->
                        variance subNvType superNvType newVisited state

                    | NonvariableType subNvType when not (subNvType |> isObject) ->
                        super subNvType superNvType newVisited state

                    | _ -> Error "Subtype relation does not hold")


and variance // ğš« âŠ¢ C<TÌ„> <: C<UÌ„>
    (subtype: NonvariableType) // C<TÌ„>
    (supertype: NonvariableType) // C<UÌ„>
    (visited: Visited)
    (state: State)
    =
    let _, classTable, _ = state

    let varianceHolds // ğš« âŠ¢ Ti <:var(C#i) Ui
        (subtypeArgument: Type) // Ti
        (supertypeArgument: Type) // Ui
        (var: Variance) // var(C#i)
        ()
        =
        match var with
        | Invariant ->
            sRefl subtypeArgument supertypeArgument // ğš« âŠ¢ Ti = Ui
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚€ '{supertypeArgument |> debugType}':"

        | Covariant ->
            subtypeAssertion subtypeArgument supertypeArgument visited state // ğš« âŠ¢ Ti <: Ui
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚Š '{supertypeArgument |> debugType}':"

        | Contravariant ->
            subtypeAssertion supertypeArgument subtypeArgument visited state // ğš« âŠ¢ Ui <: Ti
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚‹ '{supertypeArgument |> debugType}':"

    let folder
        (state: Result<unit, string>)
        (subtypeArgument: Type, // Ti
         supertypeArgument: Type, // Ui
         typeParam: TypeParameter) // C#i
        =
        state
        |> Result.bind (varianceHolds subtypeArgument supertypeArgument typeParam.Variance)


    // Check if C = Object first, as it doesn't exist in the class table
    wfObject subtype
    |> orElse (fun _ ->
        classTable
        |> ClassTable.find subtype.ClassName
        |> Result.bind (fun classDef ->
            (Ok(), List.zip3 subtype.TypeArguments supertype.TypeArguments classDef.TypeParameters)
            ||> List.fold folder))



and super // ğš« âŠ¢ C<TÌ„> <: D<UÌ„>
    (subtype: NonvariableType) // C<TÌ„>
    (supertype: NonvariableType) // D<UÌ„>
    (visited: Visited)
    (state: State)
    =
    let _, classTable, _ = state

    classTable
    |> ClassTable.find subtype.ClassName
    |> Result.bind (fun subtypeClassDef -> // C<XÌ„>
        subtypeClassDef.Superclass // V
        |> substituteInNvType subtype.TypeArguments subtypeClassDef.TypeParameters)
    |> Result.bind (fun substitutedSuperClass -> // [TÌ„/XÌ„]V
        // [TÌ„/XÌ„]V <: D<UÌ„>
        subtypeAssertion (NonvariableType substitutedSuperClass) (NonvariableType supertype) visited state)

and bound // ğš« âŠ¢ X <: D<UÌ„>
    (subtype: TypeVariableName) // X
    (supertype: NonvariableType) // D<UÌ„>
    (visited: Visited)
    (state: State)
    =
    let typeEnv, _, _ = state

    match typeEnv |> List.tryFind (fun tp -> tp.Name = subtype) with
    | None -> Error $"Type variable '{subtype |> typeVariableNameString}' not defined"
    | Some { Bound = bound } -> // ğš«(X)
        // ğš«(X) <: D<UÌ„>
        subtypeAssertion (NonvariableType bound) (NonvariableType supertype) visited state
