module TypeCheck.SubtypeAssertion

open AST
open ClassTable
open Utils
open TypeCheck.SRefl

let rec subtypeAssertion // ğš« âŠ¢ T <: U
    (subtype: Type) // T
    (supertype: Type) // U
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    printfn $"{subtype |> debugType} <: {supertype |> debugType}"

    // First, check if S-Refl holds
    sRefl subtype supertype
    |> orElse (fun _ ->
        match supertype with
        | TypeVariable _ -> Error "Bounds cannot be type variables"
        | NonvariableType superNvType -> // U = D<UÌ„>
            match subtype with
            // If T = Object, return Error, as we now U isn't Object as S-Refl doesn't hold
            | NonvariableType subNvType when subNvType |> isObject -> Error "'Object' does not have a supertype"

            // If T = C<TÌ„> and C = D, check if Var rule holds
            | NonvariableType subNvType when subNvType.ClassName = superNvType.ClassName ->
                variance subNvType superNvType typeEnv classTable

            | NonvariableType subNvType -> super subNvType superNvType typeEnv classTable

            | TypeVariable subtypeVar -> bound subtypeVar superNvType typeEnv classTable)

and variance // ğš« âŠ¢ C<TÌ„> <: C<UÌ„>
    (subtype: NonvariableType) // C<TÌ„>
    (supertype: NonvariableType) // C<UÌ„>
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    let varianceHolds // ğš« âŠ¢ Ti <:var(C#i) Ui
        (subtypeArgument: Type) // Ti
        (supertypeArgument: Type) // Ui
        (var: Variance) // var(C#i)
        ()
        =
        match var with
        | Invariant ->
            sRefl subtypeArgument supertypeArgument // ğš« âŠ¢ Ti = Ui
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚€ '{supertypeArgument |> debugType}':"

        | Covariant ->
            subtypeAssertion subtypeArgument supertypeArgument typeEnv classTable // ğš« âŠ¢ Ti <: Ui
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚Š '{supertypeArgument |> debugType}':"

        | Contravariant ->
            subtypeAssertion supertypeArgument subtypeArgument typeEnv classTable // ğš« âŠ¢ Ui <: Ti
            |> prefixError $"Error in '{subtypeArgument |> debugType}' <:â‚‹ '{supertypeArgument |> debugType}':"

    let folder
        (state: Result<unit, string>)
        (subtypeArgument: Type, // Ti
         supertypeArgument: Type, // Ui
         typeParam: TypeParameter) // C#i
        =
        state
        |> Result.bind (varianceHolds subtypeArgument supertypeArgument typeParam.Variance)

    classTable
    |> ClassTable.find subtype.ClassName
    |> Result.bind (fun classDef ->
        (Ok(), List.zip3 subtype.TypeArguments supertype.TypeArguments classDef.TypeParameters)
        ||> List.fold folder)

and super // ğš« âŠ¢ C<TÌ„> <: D<UÌ„>
    (subtype: NonvariableType) // C<TÌ„>
    (supertype: NonvariableType) // D<UÌ„>
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    classTable
    |> ClassTable.find subtype.ClassName
    |> Result.bind (fun subtypeClassDef -> // C<XÌ„>
        subtypeClassDef.Superclass // V
        |> substituteInNvType subtype.TypeArguments subtypeClassDef.TypeParameters)
    |> Result.bind (fun substitutedSuperClass -> // [TÌ„/XÌ„]V
        // [TÌ„/XÌ„]V <: D<UÌ„>
        subtypeAssertion (NonvariableType substitutedSuperClass) (NonvariableType supertype) typeEnv classTable)

and bound // ğš« âŠ¢ X <: D<UÌ„>
    (subtype: TypeVariableName) // X
    (supertype: NonvariableType) // D<UÌ„>
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    match typeEnv |> List.tryFind (fun tp -> tp.Name = subtype) with
    | None -> Error $"Type variable '{subtype |> typeVariableNameString}' not defined"
    | Some { Bound = bound } -> // ğš«(X)
        // ğš«(X) <: D<UÌ„>
        subtypeAssertion (NonvariableType bound) (NonvariableType supertype) typeEnv classTable
