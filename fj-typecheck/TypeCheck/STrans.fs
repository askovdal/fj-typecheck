module TypeCheck.STrans

open AST
open ClassTable
open Utils
open TypeCheck.SClass
open TypeCheck.SVar
open TypeCheck.SRefl

// Returns either the type variable's bound or the class' superclass.
let tryFindTypeBound (typeDef: Type) (typeEnv: TypeParameter list) (classTable: ClassTable) =
    match typeDef with
    | TypeVariable typeVariableName ->
        match typeEnv |> List.tryFind (fun tp -> tp.Name = typeVariableName) with
        | None -> Error $"Type variable '{typeVariableName |> typeVariableNameString}' not defined"
        | Some typeParameter -> Ok typeParameter.Bound

    | NonvariableType nonvariableType ->
        classTable
        |> ClassTable.find nonvariableType.ClassName
        |> Result.bind (fun nvTypeClassDef ->
            nvTypeClassDef.Superclass
            |> substituteInNvType nonvariableType.TypeArguments nvTypeClassDef.TypeParameters)

let rec sTrans // ğš« âŠ¢ S <: U
    (subType: Type) // S
    (superType: NonvariableType) // U (nvType because bounds cannot be type variables)
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    tryFindTypeBound subType typeEnv classTable
    |> Result.bind (fun bound -> // T
        // If T is C<TÌ„> and U is C<UÌ„>, check if Variance rule holds
        if bound.ClassName = superType.ClassName then
            variance bound superType typeEnv classTable
        else
            // Check if ğš« âŠ¢ T <: U holds using S-Class
            sClass bound superType classTable
            |> orElse (fun _ ->
                classTable
                |> ClassTable.find bound.ClassName
                |> Result.bind (fun boundClassDef ->
                    // If T's bound is Object, return Error, as we know U isn't Object as S-Class does not hold
                    if boundClassDef.Superclass.ClassName |> isObject then
                        Error "S-Trans does not hold"
                    else
                        // Check if ğš« âŠ¢ T <: U holds using S-Trans
                        sTrans (NonvariableType bound) superType typeEnv classTable)))

and variance // ğš« âŠ¢ C<TÌ„> <: C<UÌ„>
    (subType: NonvariableType) // C<TÌ„>
    (superType: NonvariableType) // C<UÌ„>
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    let varianceHolds // ğš« âŠ¢ Ti <:var(C#i) Ui
        (subTypeArgument: Type) // Ti
        (superTypeArgument: Type) // Ui
        (var: Variance) // var(C#i)
        ()
        =
        match var with
        | Invariant ->
            sRefl subTypeArgument superTypeArgument // ğš« âŠ¢ Ti = Ui
            |> prefixError $"Error in '{subTypeArgument |> debugType}' <:â‚€ '{superTypeArgument |> debugType}':"

        | Covariant ->
            checkSubTypeRelation subTypeArgument superTypeArgument typeEnv classTable // ğš« âŠ¢ Ti <: Ui
            |> prefixError $"Error in '{subTypeArgument |> debugType}' <:â‚Š '{superTypeArgument |> debugType}':"

        | Contravariant ->
            checkSubTypeRelation superTypeArgument subTypeArgument typeEnv classTable // ğš« âŠ¢ Ui <: Ti
            |> prefixError $"Error in '{subTypeArgument |> debugType}' <:â‚‹ '{superTypeArgument |> debugType}':"

    let folder
        (state: Result<unit, string>)
        (subTypeArgument: Type, // Ti
         superTypeArgument: Type, // Ui
         typeParam: TypeParameter) // C#i
        =
        state
        |> Result.bind (varianceHolds subTypeArgument superTypeArgument typeParam.Variance)

    classTable
    |> ClassTable.find subType.ClassName
    |> Result.bind (fun classDef ->
        (Ok(), List.zip3 subType.TypeArguments superType.TypeArguments classDef.TypeParameters)
        ||> List.fold folder)

and checkSubTypeRelation // ğš« âŠ¢ T <: U
    (subType: Type) // T
    (superType: Type) // U
    (typeEnv: TypeParameter list) // ğš«
    (classTable: ClassTable)
    =
    // First, check if S-Refl holds
    sRefl subType superType
    |> orElse (fun _ ->
        match superType with
        | TypeVariable _ -> Error "Bounds cannot be type variables"
        | NonvariableType superNvType ->
            match subType with
            // If T is X, check if S-Var holds
            | TypeVariable subTypeVariable -> sVar subTypeVariable superNvType typeEnv

            // If T is C<TÌ„>...
            | NonvariableType subNvType ->
                // ...and U is C<UÌ„>...
                if subNvType.ClassName = superNvType.ClassName then
                    // ...check if Variance rule holds.
                    variance subNvType superNvType typeEnv classTable
                else
                    // Else, check if S-Class holds
                    sClass subNvType superNvType classTable

            // Lastly, check if S-Trans holds
            |> orElse (fun _ -> sTrans subType superNvType typeEnv classTable))
